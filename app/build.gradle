plugins {
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
    id 'idea'
    id 'java'
    id 'application'
    id 'maven-publish'
}
/****************************************
 * Local Configuration
 ****************************************/

group = "$hsvGroup"
version = "$hsvVersion"

def artifactName = "$hsvArtifactName"


repositories {
    mavenCentral()
    mavenLocal()
    // Nexus Releases
    maven {
        url = "$nexusReleasesUrl"
        allowInsecureProtocol = true
        credentials {
            username = "$nexusUser"
            password = "$nexusPassword"
        }
    }
    // Nexus Snapshots
    maven {
        url = "$nexusSnapshotsUrl"
        allowInsecureProtocol = true
        credentials {
            username = "$nexusUser"
            password = "$nexusPassword"
        }
    }
}

dependencies {

    // HeaderSV Modules
    implementation(project(":rest"))
    implementation(project(":core"))

    // logging
    implementation 'org.slf4j:slf4j-api:1.7.26'

    // JUnit
    testImplementation 'org.junit.jupiter:junit-jupiter-api'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine'

    // logback
    testImplementation "ch.qos.logback:logback-classic"

    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'

}

mainClassName = "io.bitcoinsv.headerSV.app.HeaderSVApplication"
bootJar {
    enabled = true
    archiveFileName = "${artifactName}-${version}.jar"
    metaInf {
        from "${project.rootDir}/LICENSE"
    }
}



jar {
    enabled = false
}

test {
    useJUnitPlatform()
}



/****************************************
 * Publish the JAR
 ****************************************/
publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = "$artifactName"
            groupId = "$group"
            version = "$version"

            from components.java

            pom {
                name = "$artifactName"
                description = 'A Service to synchronize with the BSV Chain and retrive Info from it'
                url = 'https://github.com/bitcoin-sv/block-headers-client/core'

                licenses {
                    license {
                        name = 'Open BSV License'
                        url = 'https://github.com/bitcoin-sv/block-headers-client/core/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        organization = 'nChain'
                        organizationUrl = 'https://nchain.com'
                        name = 'm.fletcher'
                    }
                    developer {
                        organization = 'nChain'
                        organizationUrl = 'https://nchain.com'
                        name = 'i.fernandez'
                    }
                }
                scm {
                    url = 'https://github.com/bitcoin-sv/block-headers-client'
                }
            }
        }
    }

    repositories {
        maven {
            allowInsecureProtocol = true
            credentials {
                username = "$rootProject.nexusUser"
                password = "$rootProject.nexusPassword"
            }
            if(project.version.endsWith('-SNAPSHOT')) {
                url "$rootProject.nexusSnapshotsUrl"
            } else {
                url "$rootProject.nexusReleasesUrl"
            }
        }
    }
}
